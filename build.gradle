import org.apache.tools.ant.filters.EscapeUnicode
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id "org.sonarqube" version "2.6.1"
    id "org.owasp.dependencycheck" version "3.3.1"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'jacoco'


def externalFileName = "$projectDir/.Thales"

if (file(externalFileName).exists()) {
    apply from: "external.gradle"
} else {
    apply from: "monext.gradle"
}

description = 'payment-method-slimpay'
group = 'com.payline.payment.slimpay'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

ext {
    paymentMethodApiVersion = '1.0'
    paymentMethodIntegrationVersion = '1.0'
    mockitoVersion = '2.19.0'
    releaseDirectoryPath = 'D:/build'
}

configurations {
    fatJarTest
}

dependencies {
    compileOnly("com.payline:payment-method-api:$paymentMethodApiVersion")

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.2'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.10'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'

    implementation group: 'com.slimpay', name: 'hapiclient', version: '1.0.2'
    //powermock ??
    testImplementation group: 'org.powermock', name: 'powermock-mockito-release-full', version: '1.5.4'

    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.21.0'

    testImplementation "com.payline:payment-method-api:${paymentMethodApiVersion}"
    testImplementation "com.payline:payment-method-integration:${paymentMethodIntegrationVersion}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.3'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '3.13.0'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '3.13.0'

    // fatJarTest include testImplementation dependencies (not for IT)
    fatJarTest group: 'org.mockito', name: 'mockito-core', version: '2.21.0'
    fatJarTest group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.3'
    fatJarTest group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '3.13.0'
    fatJarTest group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '3.13.0'
}

dependencyCheck {
    format 'ALL'
}

assemble.dependsOn shadowJar

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency('com.payline:payment-method-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-api:2.11.0'))
        exclude(dependency('org.apache.logging.log4j:log4j-core:2.11.0'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-params:5.0.3'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-api:5.0.3'))
        exclude(dependency('org.powermock:powermock-api-mockito2:2.0.0-RC.4'))
        exclude(dependency('org.powermock:powermock-module-junit4:1.7.4'))
        exclude(dependency('org.powermock:powermock-module-junit4-rule-agent:1.7.4'))
        exclude(dependency('org.powermock:powermock-module-junit4-rule:1.7.4'))
        exclude(dependency('org.seleniumhq.selenium:selenium-server:3.13.0'))
        exclude(dependency('org.seleniumhq.selenium:selenium-support:3.13.0'))
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Build-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}

task buildToLocal(type: Copy) {
    dependsOn shadowJar
    from shadowJar
    into "${releaseDirectoryPath}"
}

task printVersion {
    doLast {
        println version
    }
}

/* Replace tokens in resources files after compilation */
processResources {
    filesMatching('messages_fr.properties') {
        filter(EscapeUnicode)
    }
    filesMatching('release.properties') {
        filter(ReplaceTokens, tokens: [
                'version': project.version.replaceFirst("-.*", ""),
                'date'   : new Date().format("dd/MM/yyyy")
        ])
    }
}

// To create a JAR including test classes and dependencies
task testJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn 'shadowJar', 'testClasses'
    classifier = 'tests'
    from sourceSets.test.output.classesDirs + shadowJar
    configurations = [project.configurations.fatJarTest]
}

test.exclude '**/*IT.class'
